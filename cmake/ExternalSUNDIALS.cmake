# Copyright Amazon.com, Inc. or its affiliates. All Rights Reserved.
# SPDX-License-Identifier: Apache-2.0

#
# Build SUNDIALS
#

# Force build order
set(SUNDIALS_DEPENDENCIES)

set(SUNDIALS_OPTIONS ${PALACE_SUPERBUILD_DEFAULT_ARGS})
list(APPEND SUNDIALS_OPTIONS
  "-DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}"
  "-DCMAKE_CXX_FLAGS=${CMAKE_CXX_FLAGS}"
  "-DCMAKE_C_COMPILER=${CMAKE_C_COMPILER}"
  "-DCMAKE_C_FLAGS=${CMAKE_C_FLAGS}"
  "-DEXAMPLES_ENABLE_C=OFF"
  "-DEXAMPLES_ENABLE_CXX=OFF"
  "-DEXAMPLES_ENABLE_CUDA=OFF"
  "-DEXAMPLES_INSTALL=OFF"
  "-DENABLE_MPI=ON"
)

if(PALACE_WITH_OPENMP)
  list(APPEND SUNDIALS_OPTIONS
    "-DENABLE_OPENMP=ON"
  )
else()
  list(APPEND SUNDIALS_OPTIONS
    "-DENABLE_OPENMP=OFF"
  )
endif()

# Configure LAPACK dependency
if(NOT "${BLAS_LAPACK_LIBRARIES}" STREQUAL "")
  list(APPEND SUNDIALS_OPTIONS
    "-DENABLE_LAPACK=ON"
    "-DLAPACK_LIBRARIES=${BLAS_LAPACK_LIBRARIES}"
    "-DLAPACK_WORKS:BOOL=TRUE"
  )
endif()

if(PALACE_WITH_CUDA)
  list(APPEND SUNDIALS_OPTIONS
    "-DCMAKE_CUDA_ARCHITECTURES=${CMAKE_CUDA_ARCHITECTURES}"
    "-DENABLE_CUDA=ON"
  )
endif()

string(REPLACE ";" "; " SUNDIALS_OPTIONS_PRINT "${SUNDIALS_OPTIONS}")
message(STATUS "SUNDIALS_OPTIONS: ${SUNDIALS_OPTIONS_PRINT}")


include(ExternalProject)
ExternalProject_Add(sundials
  DEPENDS           ${SUNDIALS_DEPENDENCIES}
  GIT_REPOSITORY    ${EXTERN_SUNDIALS_URL}
  GIT_TAG           ${EXTERN_SUNDIALS_GIT_TAG}
  SOURCE_DIR        ${CMAKE_BINARY_DIR}/extern/sundials
  BINARY_DIR        ${CMAKE_BINARY_DIR}/extern/sundials-build
  INSTALL_DIR       ${CMAKE_INSTALL_PREFIX}
  PREFIX            ${CMAKE_BINARY_DIR}/extern/sundials-Cmake
  UPDATE_COMMAND    ""
  CONFIGURE_COMMAND ${CMAKE_COMMAND} <SOURCE_DIR> "${SUNDIALS_OPTIONS}"
  TEST_COMMAND      ""
)
